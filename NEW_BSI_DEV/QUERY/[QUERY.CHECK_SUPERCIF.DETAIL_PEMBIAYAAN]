DECLARE @sql_query varchar(max)
CREATE TABLE #NOMORCIF
(
	NOMORCIF VARCHAR(100)
)

CREATE TABLE #SUPERCIF
(
	SUPERCIF VARCHAR(100)
)

INSERT INTO #SUPERCIF VALUES ('52496430'), ('83193017')

INSERT INTO #NOMORCIF
SELECT NOMORCIF
FROM [NEW_BSI_DEV].[dbo].[PORTOFOLIO.ONBS.CIF_DETAIL]
WHERE SUPERCIF IN (SELECT SUPERCIF FROM #SUPERCIF)
OR NOMORCIF IN (SELECT SUPERCIF FROM #SUPERCIF)

CREATE TABLE #RESULT_TABLE
	(
		[LEGACY] VARCHAR(255)
		,[NOLOAN] VARCHAR(255)
		,[NOMORCIF] VARCHAR(255)
		,[SUPERCIF] VARCHAR(255)
		,[NAMANASABAH] VARCHAR(255)
		,[KODEOUTLET] VARCHAR(255)
		,[NAMAOUTLET] VARCHAR(255)
		,[AREA] VARCHAR(255)
		,[REGION] VARCHAR(255)
		,[NO_HP] VARCHAR(255)
		,[NO_TELP] VARCHAR(255)
		,[KTP] VARCHAR(255)
		,[NPWP] VARCHAR(255)
		,[TANGGAL_LAHIR] DATE
		,[TANGGAL_AKTIF_AKHIR] DATE
		,[OS_POKOK_PSAK_CURRENT] DECIMAL(20, 2)
		,[KOL] VARCHAR(255)
		,[TGL_WO] DATE
		,[DIVISI] VARCHAR(255)
		,[PRODUK] VARCHAR(255)
	)

INSERT INTO #RESULT_TABLE
	(
		[LEGACY]
		,[NOLOAN]
		,[NOMORCIF]
		,[KODEOUTLET]
		,[DIVISI]
		,[PRODUK]
	)
SELECT
	[LEGACY]
	,[NOLOAN]
	,[NOMORCIF]
	,[KODEOUTLET]
	,[DIVISI]
	,[PRODUK]
FROM [NEW_BSI_DEV].[dbo].[PORTOFOLIO.ONBS.LOAN_DETAIL]
WHERE [NOMORCIF] IN (SELECT NOMORCIF FROM #NOMORCIF)


SET @sql_query = 'MERGE #RESULT_TABLE [TARGET]
USING
	(
		SELECT
		[Source Data Current]  AS [LEGACY]
		,[NOLOAN]
		,[CIF] AS [NOMORCIF]
		,[Kode Outlet BSI] AS [KODEOUTLET]
		,[Divisi] AS [DIVISI]
		,[Produk BSI] AS [PRODUK]
		FROM [NEW_BSI_COLLECTION].[dbo].[PORTOFOLIO_WO.GENERATED.JOIN.WO_CURRENT]
		WHERE [CIF] IN (SELECT NOMORCIF FROM #NOMORCIF)
	) [SOURCE]
	ON [TARGET].[LEGACY] = [SOURCE].[LEGACY]
	AND [TARGET].[NOLOAN] = [SOURCE].[NOLOAN] 
WHEN NOT MATCHED BY TARGET THEN
INSERT
	(
		[LEGACY]
		,[NOLOAN]
		,[NOMORCIF]
		,[KODEOUTLET]
		,[DIVISI]
		,[PRODUK]
	)
VALUES
	(
		[SOURCE].[LEGACY]
		,[SOURCE].[NOLOAN]
		,[SOURCE].[NOMORCIF]
		,[SOURCE].[KODEOUTLET]
		,[SOURCE].[DIVISI]
		,[SOURCE].[PRODUK]
	);'

EXEC(@sql_query)


UPDATE #RESULT_TABLE
SET
	[SUPERCIF] = [SOURCE].[SUPERCIF]
	,[NAMANASABAH] = [SOURCE].[NAMANASABAH]
	,[NO_HP] = [SOURCE].[NO_HP]
	,[NO_TELP] = [SOURCE].[NO_TELP]
	,[KTP] = [SOURCE].[KTP]
	,[NPWP] = [SOURCE].[NPWP]
	,[TANGGAL_LAHIR] = [SOURCE].[TANGGAL_LAHIR]
FROM #RESULT_TABLE [TARGET]
LEFT JOIN
	(
		SELECT
			NOMORCIF
			,SUPERCIF
			,NAMANASABAH
			,NO_HP
			,NO_TELP
			,KTP
			,NPWP
			,TANGGAL_LAHIR
		FROM [NEW_BSI_DEV].[dbo].[PORTOFOLIO.ONBS.CIF_DETAIL]
		WHERE [NOMORCIF] IN (SELECT NOMORCIF FROM #NOMORCIF)
		OR [SUPERCIF] IN (SELECT NOMORCIF FROM #NOMORCIF)
	) [SOURCE]
	ON [TARGET].[NOMORCIF] = [SOURCE].[NOMORCIF]

UPDATE #RESULT_TABLE
	SET
		[NAMAOUTLET] = COALESCE([SOURCE].[Nama Outlet], [TARGET].[NAMAOUTLET])
		,[AREA] = COALESCE([SOURCE].[Nama Area], [TARGET].[AREA])
		,[REGION] = COALESCE([SOURCE].[Nama RO], [TARGET].[REGION])
	FROM #RESULT_TABLE [TARGET]
	LEFT JOIN [NEW_BSI_COLLECTION].[dbo].[PORTOFOLIO_JOIN.MAPPING.JOIN.CABANG_BSI_UPDATE] [SOURCE]
		ON [TARGET].[KODEOUTLET] = [SOURCE].[kode outlet]

UPDATE #RESULT_TABLE
	SET
		[TANGGAL_AKTIF_AKHIR] = [SOURCE].[TGL_AKTIF_AKHIR]
		,[DIVISI] = COALESCE([TARGET].[DIVISI], [NEW_BSI_COLLECTION].[dbo].[fx_adjustment_divisi_bsi]([SOURCE].[DIVISI]))
	FROM #RESULT_TABLE [TARGET]
	LEFT JOIN [NEW_BSI_COLLECTION].[dbo].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.TGL_AKTIF] [SOURCE]
		ON [TARGET].[LEGACY] = [SOURCE].[LEGACY]
		AND [TARGET].[NOLOAN] = [SOURCE].[NOLOAN]

UPDATE #RESULT_TABLE
	SET
		[OS_POKOK_PSAK_CURRENT] = [SOURCE].[(RCG) OS_POKOK_PSAK_CURRENT]
		,[KOL] = [SOURCE].[(RCG) KOL_CIF_PECAH]
		,[DIVISI] = COALESCE([TARGET].[DIVISI], [SOURCE].[(RCG) DIVISI_BSI])
		,[PRODUK] = COALESCE([TARGET].[PRODUK], [SOURCE].[(EDA) PRODUK])
	FROM #RESULT_TABLE [TARGET]
	LEFT JOIN [NEW_BSI_COLLECTION].[dbo].[PORTOFOLIO_ONBS.GENERATED.JOIN.PORTOFOLIO_CURRENT] [SOURCE]
	--LEFT JOIN [NEW_BSI_STORAGE].[dbo].[BSI.PORTOFOLIO_DAILY.2022-09-30 (QC)] [SOURCE]
		ON [TARGET].[LEGACY] = [SOURCE].[(RCG) SOURCE_CURRENT]
		AND [TARGET].[NOLOAN] = [SOURCE].[(SOURCE) NOLOAN_CURRENT]
		AND [TARGET].[NOMORCIF] = [SOURCE].[(SOURCE) NOMORCIF_CURRENT]
		AND [SOURCE].[(RCG) KOL_CIF_PECAH] NOT IN ('Lunas', 'Suspect', 'WO')

UPDATE #RESULT_TABLE
	SET
		[TANGGAL_AKTIF_AKHIR] = COALESCE([SOURCE].[Tgl Current], [TARGET].[TANGGAL_AKTIF_AKHIR])
		,[OS_POKOK_PSAK_CURRENT] = COALESCE([SOURCE].[OS Pokok Current Conversion], [TARGET].[OS_POKOK_PSAK_CURRENT])
		,[KOL] = 'WO'
		,[TGL_WO] = [SOURCE].[Tgl WO Tgl Hapus Buku]
		,[DIVISI] = COALESCE([TARGET].DIVISI, [SOURCE].[Divisi])
		,[PRODUK] = COALESCE([TARGET].PRODUK, [SOURCE].[Produk BSI])
	FROM #RESULT_TABLE [TARGET]
	LEFT JOIN [NEW_BSI_COLLECTION].[dbo].[PORTOFOLIO_WO.GENERATED.JOIN.WO_CURRENT] [SOURCE]
		ON [TARGET].[NOLOAN] = [SOURCE].[NOLOAN]
		AND [TARGET].[NOMORCIF] = [SOURCE].[CIF]
WHERE [SOURCE].[NOLOAN] IS NOT NULL

SELECT
	MAX(TANGGAL_AKTIF_AKHIR) AS TANGGAL_AKTIF_AKHIR
	,SUM(OS_POKOK_PSAK_CURRENT) AS OS_POKOK_PSAK_CURRENT
	,MAX(KOL) AS KOL
	,[LEGACY]
	,[NOMORCIF]
	,[SUPERCIF]
	,MAX(NAMANASABAH) AS NAMANASABAH
	,MAX([AREA]) AS AREA
	,MAX([REGION]) AS REGION
	,MAX(NO_HP) AS NO_HP
	,MAX(NO_TELP) AS NO_TELP
	,MAX(KTP) AS KTP
	,MAX(NPWP) AS NPWP
	,MAX(TANGGAL_LAHIR) AS TANGGAL_LAHIR
	,MAX(TGL_WO) AS TANGGAL_WO
	,MAX([DIVISI].[DIVISI]) AS DIVISI
	,MAX([DIVISI].[PRODUK]) AS PRODUK
FROM #RESULT_TABLE [SOURCE]
OUTER APPLY (
	SELECT TOP 1
		[DIVISI]
		,[PRODUK]
	FROM #RESULT_TABLE
	WHERE
		[LEGACY] = [SOURCE].[LEGACY]
		AND [NOMORCIF] = [SOURCE].[NOMORCIF]
	GROUP BY
		[DIVISI]
		,[PRODUK]
	ORDER BY
		SUM(OS_POKOK_PSAK_CURRENT) DESC
		,MAX(TANGGAL_AKTIF_AKHIR) DESC
) [DIVISI]
GROUP BY
	[LEGACY]
	,[NOMORCIF]
	,[SUPERCIF]
ORDER BY
	CASE WHEN NOMORCIF != SUPERCIF THEN 1 ELSE 0 END DESC
	,MAX(TANGGAL_AKTIF_AKHIR) DESC
	,SUM(OS_POKOK_PSAK_CURRENT) DESC
	,MAX(TANGGAL_LAHIR) DESC
	,MAX(NPWP) DESC
	,MAX(KTP) DESC
	,MAX(NO_HP) DESC
