DECLARE
	@SQL_QUERY VARCHAR(max)
	,@TGL_DATA DATE = '2023-08-01'
	,@POINTER INT = 1
	,@TARGET_CUR VARCHAR(max)
	,@TARGET_LM VARCHAR(max)
	,@TARGET_CRG VARCHAR(max);

WHILE @POINTER <= 3
BEGIN
	SET @TARGET_CUR = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) <= @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(FOG)') THEN 0.5
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @TARGET_LM = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) < @TGL_DATA
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) = EOMONTH(TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE))
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(FOG)') THEN 0.5
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @TARGET_CRG = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'CRG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) <= @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(FOG)') THEN 0.5
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @SQL_QUERY = CONCAT('DROP TABLE [series].[RCG.REKAP_OUTLET.', @TGL_DATA, CASE WHEN EOMONTH(@TGL_DATA) = @TGL_DATA THEN ' (FINAL)' END, '];');
	
	BEGIN TRY
		EXEC(@SQL_QUERY);
	END TRY
	BEGIN CATCH
	END CATCH
	
	SET @SQL_QUERY = CONCAT(
		'SELECT
			TRY_CAST(''', @TGL_DATA, ''' AS DATE) [TGL_DATA]
			,COALESCE([CUR].[KODECABANG], [LM].[KODECABANG]) [KODECABANG]
			,COALESCE([CUR].[NAMACABANG], [LM].[NAMACABANG]) [NAMACABANG]
			,COALESCE([CUR].[AREA], [LM].[AREA]) [AREA]
			,COALESCE([CUR].[REGION], [LM].[REGION]) [REGION]
			,COALESCE([CUR].[DIVISI], [LM].[DIVISI]) [DIVISI]
			,COALESCE([CUR].[PRODUK], [LM].[PRODUK]) [PRODUK]
			,COALESCE([CUR].[PRODUK_DETAIL], [LM].[PRODUK_DETAIL]) [PRODUK_DETAIL]
			,CASE
				WHEN [CRG].[Data_Restru_LSMK] IN (''Data Restru LSMK'') THEN ''Restruktur Reguler''
				WHEN [CRG].[Data_Restru_LSMK] IN (''Data Restru LSMK Covid-19'') THEN ''Restruktur Covid''
			END [FLAG_RESTRUKTUR]
			,COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''Cair Baru'') [KOL_SUPERCIF_LAST]
			,COALESCE([CUR].[KOL_LOAN], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [KOL_LOAN]
			,COALESCE([CUR].[KOL_CIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [KOL_CIF]
			,COALESCE([CUR].[KOL_SUPERCIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [KOL_SUPERCIF]
			,MAX(CASE
				WHEN [LM].[KOL_SUPERCIF] IS NULL AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''1'') THEN ''CAIR BARU''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''1'') THEN ''STAY KOL 1''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''2'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''1'') THEN ''UPGRADE KOL 2 KE KOL 1''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''3'', ''4'', ''5'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''1'') THEN ''UPGRADE NPF KE KOL 1''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''2'') THEN ''DOWNGRADE KOL 1 KE KOL 2''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''2'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''2'') THEN ''STAY KOL 2''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''3'', ''4'', ''5'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''2'') THEN ''UPGRADE NPF KE KOL 2''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''3'', ''4'', ''5'') THEN ''DOWNGRADE KOL 1 KE NPF''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''2'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''3'', ''4'', ''5'') THEN ''DOWNGRADE KOL 2 KE NPF''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''3'', ''4'', ''5'') AND LEFT([CUR].[KOL_SUPERCIF], 1) IN (''3'', ''4'', ''5'') THEN ''STAY NPF''
				WHEN COALESCE([CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG]) IS NOT NULL THEN COALESCE([CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG])
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'') AND [CUR].[KOL_SUPERCIF] IS NULL THEN ''LUNAS KOL 1''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''2'') AND [CUR].[KOL_SUPERCIF] IS NULL THEN ''LUNAS KOL 2''
				WHEN LEFT(COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''1''), 1) IN (''3'', ''4'', ''5'') AND [CUR].[KOL_SUPERCIF] IS NULL THEN ''LUNAS NPF''
			END) [Keterangan Mutasi]
			,SUM(COALESCE([CUR].[OS_POKOK_PSAK], [CHECK_WO].[EKSEKUSI_WO])) [OS_POKOK_PSAK]
			,SUM(COALESCE([CUR].[OS_POKOK], [CHECK_WO].[EKSEKUSI_WO])) [OS_POKOK]
			,SUM(
				CASE
					WHEN [LM].[KOL_SUPERCIF] IS NOT NULL AND [CUR].[KOL_SUPERCIF] IS NOT NULL THEN [dbo].[fx_common_zerofy]([LM].[OS_POKOK] - [CUR].[OS_POKOK])
					WHEN [CUR].[KOL_SUPERCIF] IS NULL AND COALESCE([CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') IN (''Lunas'') THEN [LM].[OS_POKOK]
					WHEN [CUR].[KOL_SUPERCIF] IS NULL AND COALESCE([CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') IN (''EKSEKUSI WO'') THEN [dbo].[fx_common_zerofy]([LM].[OS_POKOK] - [CHECK_WO].[EKSEKUSI_WO])
					ELSE 0.00
				END
			) [PAY_POKOK]
			,COUNT(1) [ACCT]
		INTO [series].[RCG.REKAP_OUTLET.', @TGL_DATA, CASE WHEN EOMONTH(@TGL_DATA) = @TGL_DATA THEN ' (FINAL)' END, ']
		FROM ', @TARGET_CUR, ' [CUR]
		LEFT JOIN ', @TARGET_CRG, ' [CRG]
			ON [CUR].[NOLOAN] = [CRG].[NOLOAN]
		FULL OUTER JOIN ', @TARGET_LM, ' [LM]
			ON [CUR].[NOLOAN] = [LM].[NOLOAN]
		OUTER APPLY (
			SELECT
				MAX(CASE WHEN [NOLOAN_BARU] IS NOT NULL THEN ''BUKA TUTUP'' END) [FLAG]
			FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN]
			WHERE [NOLOAN_LAMA] = [LM].[NOLOAN]
			AND [NOCIF_LAMA] = [LM].[NOMORCIF]
			AND COALESCE([KETERANGAN], ''X'') NOT IN (''Invalid'', ''EKSEKUSI WO'')
		) [CHECK_BUKA_TUTUP]
		OUTER APPLY (
			SELECT
				MAX(CASE WHEN [NOLOAN_WO] IS NOT NULL THEN ''EKSEKUSI WO'' END) [FLAG]
				,SUM(CASE WHEN [NOLOAN_WO] IS NOT NULL THEN [BAKI_DEBET_IDR] END) [EKSEKUSI_WO]
			FROM [mapping].[PORTOFOLIO_ONBS.DATABASE.JOIN.EKSEKUSI_WO]
			WHERE [NOLOAN] = [LM].[NOLOAN]
			AND [NOCIF] = [LM].[NOMORCIF]
		) [CHECK_WO]
		OUTER APPLY (
			SELECT
				COALESCE(MAX([BY_LOAN]), MAX([BY_CIF]) , MAX([BY_SUPERCIF])) [KOL_SUPERCIF]
			FROM (
				SELECT
					CASE WHEN [NOLOAN] IN (
						SELECT
							[NOLOAN_LAMA]
						FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN]
						WHERE [NOLOAN_BARU] = [CUR].[NOLOAN]
						AND COALESCE([KETERANGAN], ''X'') NOT IN (''Invalid'')
					) OR [NOLOAN] = COALESCE([CUR].[NOLOAN], ''X'') THEN [KOL_SUPERCIF]
					WHEN [CUR].[DIVISI] IN (''Pawning'') THEN ''1'' END [BY_LOAN]
					,CASE WHEN [NOMORCIF] = [CUR].[NOMORCIF] THEN [KOL_SUPERCIF] END [BY_CIF]
					,[KOL_SUPERCIF] [BY_SUPERCIF]
				FROM ', @TARGET_LM, '
				WHERE [CUR].[SUPERCIF] = [SUPERCIF]
					AND
					(
						(
							[CUR].[DIVISI] IN (''Hasanah Card'')
							AND [DIVISI] IN (''Hasanah Card'')
						)
						OR
						(
							[CUR].[DIVISI] NOT IN (''Hasanah Card'')
							AND [DIVISI] NOT IN (''Hasanah Card'')
						)
					)
			) [SUB]
		) [M_1]
		GROUP BY
			COALESCE([CUR].[KODECABANG], [LM].[KODECABANG])
			,COALESCE([CUR].[NAMACABANG], [LM].[NAMACABANG])
			,COALESCE([CUR].[AREA], [LM].[AREA])
			,COALESCE([CUR].[REGION], [LM].[REGION])
			,COALESCE([CUR].[DIVISI], [LM].[DIVISI])
			,COALESCE([CUR].[PRODUK], [LM].[PRODUK])
			,COALESCE([CUR].[PRODUK_DETAIL], [LM].[PRODUK_DETAIL])
			,CASE
				WHEN [CRG].[Data_Restru_LSMK] IN (''Data Restru LSMK'') THEN ''Restruktur Reguler''
				WHEN [CRG].[Data_Restru_LSMK] IN (''Data Restru LSMK Covid-19'') THEN ''Restruktur Covid''
			END
			,COALESCE([LM].[KOL_SUPERCIF], [M_1].[KOL_SUPERCIF], ''Cair Baru'')
			,COALESCE([CUR].[KOL_LOAN], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'')
			,COALESCE([CUR].[KOL_CIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'')
			,COALESCE([CUR].[KOL_SUPERCIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'');'
		);	
	
	BEGIN TRY
		EXEC(@SQL_QUERY);
		PRINT CONCAT('REKAP OUTLET PER ', @TGL_DATA, 'DONE!');
	END TRY
	BEGIN CATCH
		PRINT CONCAT('REKAP OUTLET PER ', @TGL_DATA, 'ERROR! ', ERROR_MESSAGE());
	END CATCH
	
	DELETE FROM [compile].[RCG.REKAP_OUTLET.COMPILE] WHERE [TGL_DATA] = @TGL_DATA;
	
	SET @SQL_QUERY = CONCAT('INSERT INTO [compile].[RCG.REKAP_OUTLET.COMPILE] SELECT * FROM [series].[RCG.REKAP_OUTLET.', @TGL_DATA, CASE WHEN EOMONTH(@TGL_DATA) = @TGL_DATA THEN ' (FINAL)' END, '];');
	
	BEGIN TRY
		EXEC(@SQL_QUERY);
	END TRY
	BEGIN CATCH
	END CATCH
	
	SET @POINTER = @POINTER + 1;
	SET @TGL_DATA = DATEADD(DAY, 1, @TGL_DATA);
END