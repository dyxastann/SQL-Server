DECLARE @TABLE_WAS VARCHAR(MAX), @TABLE_IS VARCHAR(MAX), @POINTER INT = 1, @TGL_DATA DATE = '2023-06-21', @SQL_QUERY VARCHAR(MAX);

CREATE TABLE #RESULT (
	[TGL_WAS] DATE
	,[TGL_IS] DATE
	,[NOLOAN] VARCHAR(255)
	,[NomorCIF] VARCHAR(255)
	,[NomorCIF_Super] VARCHAR(255)
	,[NAMANASABAH] VARCHAR(255)
	,[KodeOutlet_BSI] VARCHAR(255)
	,[Nama_Outlet_BSI] VARCHAR(255)
	,[Area_BSI] VARCHAR(255)
	,[Regional_BSI] VARCHAR(255)
	,[Segmen_BSI] VARCHAR(255)
	,[Produk_BSI] VARCHAR(255)
	,[Produk_BSI_Detail] VARCHAR(255)
	,[Kol_Loan_WAS] VARCHAR(255)
	,[Kol_CIF_WAS] VARCHAR(255)
	,[Kol_SUPERCIF_WAS] VARCHAR(255)
	,[Kol_Loan_IS] VARCHAR(255)
	,[Kol_CIF_IS] VARCHAR(255)
	,[Kol_SUPERCIF_IS] VARCHAR(255)
	,[KolCIF_BulanLalu_WAS] VARCHAR(255)
	,[KolCIF_BulanLalu_IS] VARCHAR(255)
)

WHILE @POINTER <= 1
BEGIN

	SET @TABLE_WAS = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'CRG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) < @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @TABLE_IS = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'CRG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) <= @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'CRG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @SQL_QUERY = CONCAT(
		'SELECT
			[WAS].[FICMISDATE] [TGL_WAS]
			,[IS].[FICMISDATE] [TGL_IS]
			,COALESCE([IS].[NOLOAN], [WAS].[NOLOAN]) [NOLOAN]
			,COALESCE([IS].[NomorCIF], [WAS].[NomorCIF]) [NomorCIF]
			,COALESCE([IS].[NomorCIF_Super], [WAS].[NomorCIF_Super]) [NomorCIF_Super]
			,COALESCE([IS].[NAMANASABAH], [WAS].[NAMANASABAH]) [NAMANASABAH]
			,COALESCE([IS].[KodeOutlet_BSI], [WAS].[KodeOutlet_BSI]) [KodeOutlet_BSI]
			,COALESCE([IS].[Nama_Outlet_BSI], [WAS].[Nama_Outlet_BSI]) [Nama_Outlet_BSI]
			,COALESCE([IS].[Area_BSI], [WAS].[Area_BSI]) [Area_BSI]
			,COALESCE([IS].[Regional_BSI], [WAS].[Regional_BSI]) [Regional_BSI]
			,COALESCE([IS].[Segmen_BSI], [WAS].[Segmen_BSI]) [Segmen_BSI]
			,COALESCE([IS].[Produk_BSI], [WAS].[Produk_BSI]) [Produk_BSI]
			,COALESCE([IS].[Produk_BSI_Detail], [WAS].[Produk_BSI_Detail]) [Produk_BSI_Detail]
			,[WAS].[KolLoan] [Kol_Loan_WAS]
			,[WAS].[KolCIF_NonSuper] [Kol_CIF_WAS]
			,COALESCE([WAS].[KolCIF], [D_1].[KOL_SUPERCIF], ''Cair Baru'') [Kol_SUPERCIF_WAS]
			,COALESCE([IS].[KolLoan], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [Kol_Loan_IS]
			,COALESCE([IS].[KolCIF_NonSuper], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [Kol_CIF_IS]
			,COALESCE([IS].[KolCIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') [Kol_SUPERCIF_IS]
			,[WAS].[KolCIF_BulanLalu] [KolCIF_BulanLalu_WAS]
			,[IS].[KolCIF_BulanLalu] [KolCIF_BulanLalu_IS]
		FROM ', @TABLE_IS, ' [IS]
		FULL OUTER JOIN ', @TABLE_WAS, ' [WAS]
			ON [WAS].[NOLOAN] = [IS].[NOLOAN]
		OUTER APPLY (
			SELECT
				MAX(CASE WHEN [NOLOAN_BARU] IS NOT NULL THEN ''BUKA TUTUP'' END) [FLAG]
			FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN]
			WHERE [NOLOAN_LAMA] = [WAS].[NOLOAN]
			AND COALESCE([KETERANGAN], ''X'') NOT IN (''Invalid'', ''EKSEKUSI WO'')
		) [CHECK_BUKA_TUTUP]
		OUTER APPLY (
			SELECT
				MAX(CASE WHEN [NOLOAN_WO] IS NOT NULL THEN ''EKSEKUSI WO'' END) [FLAG]
				,SUM(CASE WHEN [NOLOAN_WO] IS NOT NULL THEN [BAKI_DEBET_IDR] END) [EKSEKUSI_WO]
			FROM [mapping].[PORTOFOLIO_ONBS.DATABASE.JOIN.EKSEKUSI_WO]
			WHERE [NOLOAN] = [WAS].[NOLOAN]
			AND [NOCIF] = [WAS].[NOMORCIF]
		) [CHECK_WO]
		OUTER APPLY (
			SELECT
				COALESCE(MAX([BY_LOAN]), MAX([BY_CIF]) , MAX([BY_SUPERCIF])) [KOL_SUPERCIF]
			FROM (
				SELECT
					CASE WHEN [NOLOAN] IN (
						SELECT
							[NOLOAN_LAMA]
						FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN]
						WHERE [NOLOAN_BARU] = [IS].[NOLOAN]
						AND COALESCE([KETERANGAN], ''X'') NOT IN (''Invalid'')
					) OR [NOLOAN] = COALESCE([IS].[NOLOAN], ''X'') THEN [KOLCIF] END [BY_LOAN]
					,CASE WHEN [NOMORCIF] = [IS].[NOMORCIF] THEN [KOLCIF] END [BY_CIF]
					,[KOLCIF] [BY_SUPERCIF]
				FROM ', @TABLE_WAS, '
				WHERE [IS].[NomorCIF_Super] = [NomorCIF_Super]
					AND [IS].[Segmen_BSI] = [Segmen_BSI]
			) [SUB]
		) [D_1]
		WHERE 1 = 1
		AND [WAS].[KolCIF_BulanLalu] != [IS].[KolCIF_BulanLalu]
		AND [WAS].[KolCIF] IS NOT NULL
		AND COALESCE([WAS].[KolCIF], [D_1].[KOL_SUPERCIF], ''Cair Baru'') NOT IN (''CAIR BARU'')
		AND COALESCE([IS].[KolCIF], [CHECK_BUKA_TUTUP].[FLAG], [CHECK_WO].[FLAG], ''Lunas'') NOT IN (''LUNAS'', ''Buka Tutup'', ''WO'');'
	);

	BEGIN TRY
		INSERT INTO #RESULT
		EXEC(@SQL_QUERY);
		PRINT CONCAT('EKSEKUSI DATA PER ', @TGL_DATA, ' BASED ON WAS ', @TABLE_WAS, ', IS ', @TABLE_IS);
	END TRY
	BEGIN CATCH
		PRINT CONCAT('ERROR! ', ERROR_MESSAGE());
	END CATCH
	
	SET @POINTER = @POINTER + 1;
	SET @TGL_DATA = DATEADD(DAY, 1, @TGL_DATA);
END

SELECT * FROM #RESULT;
DROP TABLE #RESULT;