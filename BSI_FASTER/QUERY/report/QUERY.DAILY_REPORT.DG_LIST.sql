DECLARE @SQL_QUERY varchar(max), @TARGET varchar(max), @TARGET_BEFORE varchar(max), @tgl_data DATE = '2023-07-31', @pointer INT = 1

BEGIN TRY
	CREATE TABLE [compile].[LIST_DG.DAILY.COMPILE] (
		[TGL_BEFORE] DATE
		,[TGL_DATA] DATE
		,[TGL_REPORT] DATE
		,[NOLOAN] VARCHAR(255)
		,[NOMORCIF] VARCHAR(255)
		,[SUPERCIF] VARCHAR(255)
		,[NAMANASABAH] VARCHAR(255)
		,[KODECABANG] VARCHAR(255)
		,[NAMACABANG] VARCHAR(255)
		,[AREA] VARCHAR(255)
		,[REGION] VARCHAR(255)
		,[DIVISI] VARCHAR(255)
		,[PRODUK] VARCHAR(255)
		,[PRODUK_DETAIL] VARCHAR(255)
		,[CYCLE] INTEGER
		,[KOL_LOAN_BEFORE] VARCHAR(255)
		,[KOL_CIF_BEFORE] VARCHAR(255)
		,[KOL_SUPERCIF_BEFORE] VARCHAR(255)
		,[KOL_LOAN_CURRENT] VARCHAR(255)
		,[KOL_CIF_CURRENT] VARCHAR(255)
		,[KOL_SUPERCIF_CURRENT] VARCHAR(255)
		,[OS_POKOK_PSAK_BEFORE] DECIMAL(20, 2)
		,[OS_POKOK_BEFORE] DECIMAL(20, 2)
		,[OS_MARGIN_BEFORE] DECIMAL(20, 2)
		,[REALISASI_BAGIHASIL_BEFORE] DECIMAL(20, 2)
		,[RBH_PBH_BEFORE] DECIMAL(20, 10)
		,[OS_POKOK_PSAK_CURRENT] DECIMAL(20, 2)
		,[OS_POKOK_CURRENT] DECIMAL(20, 2)
		,[OS_MARGIN_CURRENT] DECIMAL(20, 2)
		,[REALISASI_BAGIHASIL_CURRENT] DECIMAL(20, 2)
		,[RBH_PBH_CURRENT] DECIMAL(20, 10)
		,[FLAG_DATA] VARCHAR(255)
		,[PENYEBAB] VARCHAR(255)
	);
END TRY
BEGIN CATCH
END CATCH

WHILE @POINTER <= 7
BEGIN
	DELETE FROM [compile].[LIST_DG.DAILY.COMPILE] WHERE [TGL_REPORT] = @tgl_data;
	SET @TARGET_BEFORE = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) < @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @TARGET = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) <= @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @SQL_QUERY = CONCAT(
		'SELECT
			[BEF].[TGL_DATA] [TGL_BEFORE]
			,[CUR].[TGL_DATA] [TGL_DATA]
			,''', @tgl_Data, ''' [TGL_REPORT]
			,[CUR].[NOLOAN]
			,[CUR].[NOMORCIF]
			,[CUR].[SUPERCIF] [SUPERCIF]
			,[CUR].[NAMANASABAH] [NAMANASABAH]
			,[CUR].[KODECABANG] [KODECABANG]
			,[CUR].[NAMACABANG] [NAMACABANG]
			,[CUR].[AREA] [AREA]
			,[CUR].[REGION] [REGION]
			,[CUR].[DIVISI] [DIVISI]
			,[CUR].[PRODUK] [PRODUK]
			,[CUR].[PRODUK_DETAIL] [PRODUK_DETAIL]
			,[CUR].[CYCLE] [CYCLE]
			,[BEF].[KOL_LOAN] [KOL_LOAN_BEFORE]
			,[BEF].[KOL_CIF] [KOL_CIF_BEFORE]
			,COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF]) [KOL_SUPERCIF_BEFORE]
			,[CUR].[KOL_LOAN] [KOL_LOAN_CURRENT]
			,[CUR].[KOL_CIF] [KOL_CIF_CURRENT]
			,[CUR].[KOL_SUPERCIF] [KOL_SUPERCIF_CURRENT]
			,[BEF].[OS_POKOK_PSAK] [OS_POKOK_PSAK_BEFORE]
			,[BEF].[OS_POKOK] [OS_POKOK_BEFORE]
			,[BEF].[OS_MARGIN] [OS_MARGIN_BEFORE]
			,[BEF].[REALISASI_BAGIHASIL] [REALISASI_BAGIHASIL_BEFORE]
			,CASE 
				WHEN NULLIF([BEF].[REALISASI_BAGIHASIL], 0.00) IS NULL AND NULLIF([BEF].[PROYEKSI_BAGIHASIL], 0.00) IS NULL THEN NULL
				ELSE [BEF].[REALISASI_BAGIHASIL] / COALESCE(NULLIF([BEF].[PROYEKSI_BAGIHASIL], 0.00), NULLIF([BEF].[REALISASI_BAGIHASIL], 0.00), 1.00)
			END [RBH_PBH_BEFORE]
			,[CUR].[OS_POKOK_PSAK] [OS_POKOK_PSAK_CURRENT]
			,[CUR].[OS_POKOK] [OS_POKOK_CURRENT]
			,[CUR].[OS_MARGIN] [OS_MARGIN_CURRENT]
			,[CUR].[REALISASI_BAGIHASIL] [REALISASI_BAGIHASIL_CURRENT]
			,CASE 
				WHEN NULLIF([CUR].[REALISASI_BAGIHASIL], 0.00) IS NULL AND NULLIF([CUR].[PROYEKSI_BAGIHASIL], 0.00) IS NULL THEN NULL
				ELSE [CUR].[REALISASI_BAGIHASIL] / COALESCE(NULLIF([CUR].[PROYEKSI_BAGIHASIL], 0.00), NULLIF([CUR].[REALISASI_BAGIHASIL], 0.00), 1.00)
			END [RBH_PBH_CURRENT]
			,CASE
				WHEN (
					LEFT([CUR].[KOL_SUPERCIF], 1) IN (''2'')
					AND [CUR].[KOL_SUPERCIF] > COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF], ''1'')
				) THEN CONCAT(''Downgrade Kol 2'', CASE WHEN COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF], ''1'') IN (''2A'', ''2B'') THEN '' Down Bucket'' END,'' by SuperCIF'')
				WHEN (
					LEFT([CUR].[KOL_SUPERCIF], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'', ''2'')
				) THEN ''Downgrade NPF by SuperCIF''
				WHEN (
					LEFT([CUR].[KOL_CIF], 1) IN (''2'')
					AND [CUR].[KOL_CIF] > COALESCE([BEF].[KOL_CIF], ''1'')
				) THEN CONCAT(''Downgrade Kol 2'', CASE WHEN COALESCE([BEF].[KOL_CIF], ''1'') IN (''2A'', ''2B'') THEN '' Down Bucket'' END,'' by CIF'')
				WHEN (
					LEFT([CUR].[KOL_CIF], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_CIF], ''1''), 1) IN (''1'', ''2'')
				) THEN ''Downgrade NPF by CIF''
				WHEN (
					LEFT([CUR].[KOL_LOAN], 1) IN (''2'')
					AND [CUR].[KOL_LOAN] > COALESCE([BEF].[KOL_LOAN], ''1'')
				) THEN CONCAT(''Downgrade Kol 2'', CASE WHEN COALESCE([BEF].[KOL_LOAN], ''1'') IN (''2A'', ''2B'') THEN '' Down Bucket'' END,'' by Loan'')
				WHEN (
					LEFT([CUR].[KOL_LOAN], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_LOAN], ''1''), 1) IN (''1'', ''2'')
				) THEN ''Downgrade NPF by Loan''
			END [FLAG_DATA]
			,NULL AS [PENYEBAB]
		FROM ', @TARGET, ' [CUR]
		FULL OUTER JOIN ', @TARGET_BEFORE, ' [BEF]
			ON [CUR].[NOLOAN] = [BEF].[NOLOAN]
		OUTER APPLY (
			SELECT
				COALESCE(MAX([BY_LOAN]), MAX([BY_CIF]) , MAX([BY_SUPERCIF])) [KOL_SUPERCIF]
			FROM (
				SELECT
					CASE WHEN [NOLOAN] IN (
						SELECT
							[NOLOAN_LAMA]
						FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN]
						WHERE [NOLOAN_BARU] = [CUR].[NOLOAN]
						AND COALESCE([KETERANGAN], ''X'') NOT IN (''Invalid'')
					) OR [NOLOAN] = COALESCE([CUR].[NOLOAN], ''X'') THEN [KOL_SUPERCIF]
					WHEN [CUR].[DIVISI] IN (''Pawning'') THEN ''1'' END [BY_LOAN]
					,CASE WHEN [NOMORCIF] = [CUR].[NOMORCIF] THEN [KOL_SUPERCIF] END [BY_CIF]
					,[KOL_SUPERCIF] [BY_SUPERCIF]
				FROM ', @TARGET_BEFORE, '
				WHERE [CUR].[SUPERCIF] = [SUPERCIF]
				AND
				(
					(
						[CUR].[DIVISI] IN (''Hasanah Card'')
						AND [DIVISI] IN (''Hasanah Card'')
					)
					OR
					(
						[CUR].[DIVISI] NOT IN (''Hasanah Card'')
						AND [DIVISI] NOT IN (''Hasanah Card'')
					)
				)
			) [SUB]
		) [D_1]
		WHERE COALESCE([CUR].[DIVISI], [BEF].[DIVISI]) IN (''Konsumer'', ''Hasanah Card'', ''Pawning'', ''SME'', ''Mikro'')
		AND
			(
				(
					LEFT([CUR].[KOL_SUPERCIF], 1) IN (''2'')
					AND [CUR].[KOL_SUPERCIF] > COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF], ''1'')
				)
				OR
				(
					LEFT([CUR].[KOL_SUPERCIF], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_SUPERCIF], [D_1].[KOL_SUPERCIF], ''1''), 1) IN (''1'', ''2'')
				)
				OR
				(
					LEFT([CUR].[KOL_CIF], 1) IN (''2'')
					AND [CUR].[KOL_CIF] > COALESCE([BEF].[KOL_CIF], ''1'')
				)
				OR
				(
					LEFT([CUR].[KOL_CIF], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_CIF], ''1''), 1) IN (''1'', ''2'')
				)
				OR
				(
					LEFT([CUR].[KOL_LOAN], 1) IN (''2'')
					AND [CUR].[KOL_LOAN] > COALESCE([BEF].[KOL_LOAN], ''1'')
				)
				OR
				(
					LEFT([CUR].[KOL_LOAN], 1) IN (''3'', ''4'', ''5'')
					AND LEFT(COALESCE([BEF].[KOL_LOAN], ''1''), 1) IN (''1'', ''2'')
				)
			);'
	);
	
	INSERT INTO [compile].[LIST_DG.DAILY.COMPILE] EXEC(@SQL_QUERY);
	
	SET @tgl_data = DATEADD(DAY, 1, @tgl_data);
	SET @pointer = @pointer + 1;
END