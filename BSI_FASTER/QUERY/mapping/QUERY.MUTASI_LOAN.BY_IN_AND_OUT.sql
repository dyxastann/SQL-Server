DECLARE @TABLE_CUR VARCHAR(MAX), @TABLE_BEF VARCHAR(MAX), @SQL_QUERY VARCHAR(MAX), @TGL_DATA DATE = '2023-07-20', @POINTER INT = 1;

CREATE TABLE #OUT_ (
	[TGL_DATA] DATE
	,[NOLOAN] VARCHAR(255)
	,[NomorCIF] VARCHAR(255)
	,[SuperCIF] VARCHAR(255)
	,[NAMANASABAH] VARCHAR(255)
	,[KodeCabang] VARCHAR(255)
	,[NamaCabang] VARCHAR(255)
	,[Area] VARCHAR(255)
	,[Region] VARCHAR(255)
	,[Divisi] VARCHAR(255)
	,[Produk] VARCHAR(255)
	,[Produk_Detail] VARCHAR(255)
	,[Loantype] VARCHAR(255)
	,[Kol_Loan] VARCHAR(255)
	,[Kol_CIF] VARCHAR(255)
	,[Kol_SUPERCIF] VARCHAR(255)
	,[OS_POKOK] DECIMAL(20, 2)
	,[OS_MARGIN] DECIMAL(20, 2)
);

CREATE TABLE #IN_ (
	[TGL_DATA] DATE
	,[NOLOAN] VARCHAR(255)
	,[NomorCIF] VARCHAR(255)
	,[SuperCIF] VARCHAR(255)
	,[NAMANASABAH] VARCHAR(255)
	,[KodeCabang] VARCHAR(255)
	,[NamaCabang] VARCHAR(255)
	,[Area] VARCHAR(255)
	,[Region] VARCHAR(255)
	,[Divisi] VARCHAR(255)
	,[Produk] VARCHAR(255)
	,[Produk_Detail] VARCHAR(255)
	,[Loantype] VARCHAR(255)
	,[Kol_Loan] VARCHAR(255)
	,[Kol_CIF] VARCHAR(255)
	,[Kol_SUPERCIF] VARCHAR(255)
	,[OS_POKOK] DECIMAL(20, 2)
	,[OS_MARGIN] DECIMAL(20, 2)
);

WHILE @POINTER <= 10
BEGIN
	SET @TABLE_CUR = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) <= @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @TABLE_BEF = (
		SELECT TOP 1
			CONCAT('[', SCHEMA_NAME(SCHEMA_ID), '].[', name, ']') AS NAME
		FROM [sys].[objects]
		WHERE type_desc = 'USER_TABLE'
		AND name LIKE 'RCG.LOAN_DAILY.____-__-__%'
		AND TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) < @TGL_DATA
		ORDER BY
			TRY_CAST(LEFT(REPLACE(name, 'RCG.LOAN_DAILY.', ''), 10) AS DATE) DESC
			,CASE
				WHEN name LIKE ('%(FINAL)') THEN 1
				WHEN name LIKE ('%(QC)') THEN -1
				ELSE 0
			END DESC
	);
	
	SET @SQL_QUERY = CONCAT(
		'SELECT
			''', @TGL_DATA, ''' [TGL_DATA]
			,COALESCE([CUR].[NOLOAN], [BEF].[NOLOAN]) [NOLOAN]
			,COALESCE([CUR].[NOMORCIF], [BEF].[NOMORCIF]) [NOMORCIF]
			,COALESCE([CUR].[SUPERCIF], [BEF].[SUPERCIF]) [SUPERCIF]
			,COALESCE([CUR].[NAMANASABAH], [BEF].[NAMANASABAH]) [NAMANASABAH]
			,COALESCE([CUR].[KODECABANG], [BEF].[KODECABANG]) [KODECABANG]
			,COALESCE([CUR].[NAMACABANG], [BEF].[NAMACABANG]) [NAMACABANG]
			,COALESCE([CUR].[AREA], [BEF].[AREA]) [AREA]
			,COALESCE([CUR].[REGION], [BEF].[REGION]) [REGION]
			,COALESCE([CUR].[DIVISI], [BEF].[DIVISI]) [DIVISI]
			,COALESCE([CUR].[PRODUK], [BEF].[PRODUK]) [PRODUK]
			,COALESCE([CUR].[PRODUK_DETAIL], [BEF].[PRODUK_DETAIL]) [PRODUK_DETAIL]
			,COALESCE([CUR].[LOANTYPE], [BEF].[LOANTYPE]) [LOANTYPE]
			,COALESCE([CUR].[KOL_LOAN], [BEF].[KOL_LOAN]) [KOL_LOAN]
			,COALESCE([CUR].[KOL_CIF], [BEF].[KOL_CIF]) [KOL_CIF]
			,COALESCE([CUR].[KOL_SUPERCIF], [BEF].[KOL_SUPERCIF]) [KOL_SUPERCIF]
			,COALESCE([CUR].[OS_POKOK], [BEF].[OS_POKOK]) [OS_POKOK]
			,COALESCE([CUR].[OS_MARGIN], [BEF].[OS_MARGIN]) [OS_MARGIN]
		FROM ', @TABLE_BEF, ' [BEF]
		LEFT JOIN ', @TABLE_CUR, ' [CUR]
			ON [CUR].[NOLOAN] = [BEF].[NOLOAN]
		WHERE [CUR].[NOLOAN] IS NULL;'
	)
	
	BEGIN TRY
		INSERT INTO #OUT_
		EXEC(@SQL_QUERY);
		PRINT CONCAT('EKSEKUSI DATA RUN OFF PER ', @TGL_DATA, ' BASED ON CUR ', @TABLE_CUR, ', BEF ', @TABLE_BEF);
	END TRY
	BEGIN CATCH
		PRINT CONCAT('ERROR! ', ERROR_MESSAGE());
	END CATCH
	
	SET @SQL_QUERY = CONCAT(
		'SELECT
			''', @TGL_DATA, ''' [TGL_DATA]
			,COALESCE([CUR].[NOLOAN], [BEF].[NOLOAN]) [NOLOAN]
			,COALESCE([CUR].[NOMORCIF], [BEF].[NOMORCIF]) [NOMORCIF]
			,COALESCE([CUR].[SUPERCIF], [BEF].[SUPERCIF]) [SUPERCIF]
			,COALESCE([CUR].[NAMANASABAH], [BEF].[NAMANASABAH]) [NAMANASABAH]
			,COALESCE([CUR].[KODECABANG], [BEF].[KODECABANG]) [KODECABANG]
			,COALESCE([CUR].[NAMACABANG], [BEF].[NAMACABANG]) [NAMACABANG]
			,COALESCE([CUR].[AREA], [BEF].[AREA]) [AREA]
			,COALESCE([CUR].[REGION], [BEF].[REGION]) [REGION]
			,COALESCE([CUR].[DIVISI], [BEF].[DIVISI]) [DIVISI]
			,COALESCE([CUR].[PRODUK], [BEF].[PRODUK]) [PRODUK]
			,COALESCE([CUR].[PRODUK_DETAIL], [BEF].[PRODUK_DETAIL]) [PRODUK_DETAIL]
			,COALESCE([CUR].[LOANTYPE], [BEF].[LOANTYPE]) [LOANTYPE]
			,COALESCE([CUR].[KOL_LOAN], [BEF].[KOL_LOAN]) [KOL_LOAN]
			,COALESCE([CUR].[KOL_CIF], [BEF].[KOL_CIF]) [KOL_CIF]
			,COALESCE([CUR].[KOL_SUPERCIF], [BEF].[KOL_SUPERCIF]) [KOL_SUPERCIF]
			,COALESCE([CUR].[OS_POKOK], [BEF].[OS_POKOK]) [OS_POKOK]
			,COALESCE([CUR].[OS_MARGIN], [BEF].[OS_MARGIN]) [OS_MARGIN]
		FROM ', @TABLE_CUR, ' [CUR]
		LEFT JOIN ', @TABLE_BEF, ' [BEF]
			ON [CUR].[NOLOAN] = [BEF].[NOLOAN]
		WHERE [BEF].[NOLOAN] IS NULL;'
	)
	
	BEGIN TRY
		INSERT INTO #IN_
		EXEC(@SQL_QUERY);
		PRINT CONCAT('EKSEKUSI DATA CAIR BARU PER ', @TGL_DATA, ' BASED ON CUR ', @TABLE_CUR, ', BEF ', @TABLE_BEF);
	END TRY
	BEGIN CATCH
		PRINT CONCAT('ERROR! ', ERROR_MESSAGE());
	END CATCH
	
	SET @POINTER = @POINTER + 1;
	SET @TGL_DATA = DATEADD(DAY, 1, @TGL_DATA);
END

SELECT * FROM #OUT_;
SELECT * FROM #IN_;

WITH CTE AS
(
	SELECT
		CASE WHEN [OUT].[DIVISI] IN ('Hasanah Card') THEN 'BNIS' ELSE 'BSM' END [LEGACY]
		,[OUT].[NOLOAN] [NOLOAN]
		,[OUT].[NOMORCIF] [NOMORCIF]
		,[OUT].[KODECABANG] [KODE_OUTLET]
		,[OUT].[NAMANASABAH] [NAMANASABAH]
		,CASE WHEN [IN].[DIVISI] IN ('Hasanah Card') THEN 'BNIS' ELSE 'BSM' END [LEGACY_CURRENT]
		,[IN].[NOLOAN] [NOLOAN_BARU]
		,[IN].[NOMORCIF] [NOMORCIF_BARU]
		,[IN].[KODECABANG] [KODE_OUTLET_BARU]
		,[IN].[NAMANASABAH] [NAMANASABAH_BARU]
		,'ONBS' [FLAG_DATA]
		,[IN].[TGL_DATA] [TGL_EFEKTIF]
		,'BUKA TUTUP (SUSPECT RCG)' [KETERANGAN]
	FROM #IN_ [IN]
	LEFT JOIN #OUT_ [OUT]
		ON [IN].[SUPERCIF] = [OUT].[SUPERCIF]
		AND [IN].[OS_POKOK] = [OUT].[OS_POKOK]
		AND [IN].[OS_MARGIN] = [OUT].[OS_MARGIN]
		AND [IN].[TGL_DATA] BETWEEN DATEADD(DAY, -3, [OUT].[TGL_DATA]) AND DATEADD(DAY, 3, [OUT].[TGL_DATA])
	OUTER APPLY (
		SELECT
		 COUNT(1) [ACCT]
		FROM [mapping].[PORTOFOLIO_ONBS.COMPILE.JOIN.REKAP_NOLOAN.MUTASI_NOLOAN] [MUTASI]
		WHERE [MUTASI].[NOLOAN_LAMA] IN ([IN].[NOLOAN], [OUT].[NOLOAN])
		OR [MUTASI].[NOLOAN_BARU] IN ([IN].[NOLOAN], [OUT].[NOLOAN])
	) [MUTASI]
	WHERE [OUT].[NOLOAN] IS NOT NULL
	AND TRY_CAST(RIGHT([IN].[OS_POKOK], 4) AS DECIMAL(20, 2)) > 0.00
	AND [OUT].[NOLOAN] != [IN].[NOLOAN]
	AND COALESCE([MUTASI].[ACCT], 0) = 0
)
SELECT
	[TARGET].*
FROM CTE [TARGET]
OUTER APPLY (
	SELECT
		COUNT(1) AS [ACCT]
	FROM CTE [SOURCE]
	WHERE [TARGET].[NOLOAN] = [SOURCE].[NOLOAN]
) [TUTUP]
OUTER APPLY (
	SELECT
		COUNT(1) AS [ACCT]
	FROM CTE [SOURCE]
	WHERE [TARGET].[NOLOAN_BARU] = [SOURCE].[NOLOAN_BARU]
) [BUKA]
WHERE [BUKA].[ACCT] = 1
AND [TUTUP].[ACCT] = 1
ORDER BY
	[NOMORCIF]
	,[NOMORCIF_BARU]
	,[NOLOAN]
	,[NOLOAN_BARU];

DROP TABLE #OUT_;
DROP TABLE #IN_;